
#Область Служебные_Процедуры_И_Функции

// Управление видимостью элементов формы и отображением заголовков элементов управления.
//
&НаСервере
Процедура УправлениеВидимостью()

	// Видимость высоты в зависимости от типа пиломатериала
	//
	ВидимостьВысоты = (Объект.ТипПиломатериала = Перечисления.ТипыПиломатериалов.Доска) ИЛИ Ложь;
	ОбщегоНазначения.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаВысота", "Видимость", ВидимостьВысоты);
	
	
	// Изменение заголовка ширины в зависимости от типа пиломатериала
	//
	ЗаголовокШирины = ?(Объект.ТипПиломатериала = Перечисления.ТипыПиломатериалов.Бревно, НСтр("ru='Диаметр'"), НСтр("ru='Ширина'"));
	ОбщегоНазначения.УстановитьСвойствоЭлементаФормы(Элементы, "Ширина", "Заголовок", ЗаголовокШирины);

КонецПроцедуры // УправлениеВидимостью
 

#КонецОбласти



#Область Обработчики_Событий_Формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация нового элемента
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Настройки = ОбщегоНазначения.СохраняемыеЗначения(ИмяФормы);
		
		// Заполнение единиц измерения
		Объект.ЕдиницаВысоты 	= Константы.ЕдиницаВысоты.Получить();
		Объект.ЕдиницаДлины 	= Константы.ЕдиницаДлины.Получить();
		Объект.ЕдиницаШирины 	= Константы.ЕдиницаШирины.Получить();
		
		// Установление типа пиломатериала
		Если Настройки.Свойство("ТипПиломатериала") Тогда
			Объект.ТипПиломатериала = Настройки.ТипПиломатериала;
		Иначе
			Объект.ТипПиломатериала = Перечисления.ТипыПиломатериалов.Брус;
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеВидимостью();
	
КонецПроцедуры // ПриСозданииНаСервере

#КонецОбласти



#Область Обработчики_Событий_Элементов_Формы

&НаСервере
Процедура ТипПиломатериалаПриИзмененииНаСервере()
	
	ОбщегоНазначения.СохранитьНастройку(ИмяФормы, "ТипПиломатериала", Объект.ТипПиломатериала);
	
	УправлениеВидимостью();
	
КонецПроцедуры // ТипПиломатериалаПриИзмененииНаСервере

&НаКлиенте
Процедура ТипПиломатериалаПриИзменении(Элемент)
	ТипПиломатериалаПриИзмененииНаСервере();
КонецПроцедуры // ТипПиломатериалаПриИзменении

#КонецОбласти