
#Область СлужебныеПроцедурыИФункции

// Управление видимостью элементов формы и отображением заголовков элементов управления.
//
&НаСервере
Процедура УправлениеВидимостью()

	// Видимость высоты в зависимости от типа пиломатериала
	//
	ВидимостьВысоты = (Объект.ТипПиломатериала <> Перечисления.ТипыПиломатериалов.Бревно) ИЛИ Ложь;
	ОбщегоНазначения.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаВысота", "Видимость", ВидимостьВысоты);
	
	
	// Изменение заголовка ширины в зависимости от типа пиломатериала
	//
	ЗаголовокШирины = ?(Объект.ТипПиломатериала = Перечисления.ТипыПиломатериалов.Бревно, НСтр("ru='Диаметр'"), НСтр("ru='Ширина'"));
	ОбщегоНазначения.УстановитьСвойствоЭлементаФормы(Элементы, "Ширина", "Заголовок", ЗаголовокШирины);

КонецПроцедуры // УправлениеВидимостью
 

#КонецОбласти



#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация нового элемента
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Настройки = ОбщегоНазначения.СохраняемыеЗначения(ИмяФормы);
		
		// Заполнение единиц измерения
		Объект.ЕдиницаВысоты 	= Константы.ЕдиницаВысоты.Получить();
		Объект.ЕдиницаДлины 	= Константы.ЕдиницаДлины.Получить();
		Объект.ЕдиницаШирины 	= Константы.ЕдиницаШирины.Получить();
		
		// Установление типа пиломатериала
		Если Настройки.Свойство("ТипПиломатериала") Тогда
			Объект.ТипПиломатериала = Настройки.ТипПиломатериала;
		Иначе
			Объект.ТипПиломатериала = Перечисления.ТипыПиломатериалов.Брус;
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеВидимостью();
	
	// Проверка на предмет доступности изменений в предопределённом элементе
	//
	Если Объект.Предопределенный Тогда
		ОбщегоНазначения.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаОсновное", "ТолькоПросмотр", Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Перед записью следует сформировать наименование пиломатериала
	// согласно выбранным параметрам.
	//
	Справочники.ВидыПиломатериалов.СформироватьНаименование(ТекущийОбъект);
	
КонецПроцедуры // ПередЗаписьюНаСервере

#КонецОбласти



#Область ОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура ТипПиломатериалаПриИзмененииНаСервере()
	
	ОбщегоНазначения.СохранитьНастройку(ИмяФормы, "ТипПиломатериала", Объект.ТипПиломатериала);
	
	УправлениеВидимостью();
	
	Справочники.ВидыПиломатериалов.СформироватьНаименование(Объект);
	
КонецПроцедуры // ТипПиломатериалаПриИзмененииНаСервере

&НаКлиенте
Процедура ТипПиломатериалаПриИзменении(Элемент)
	ТипПиломатериалаПриИзмененииНаСервере();
КонецПроцедуры // ТипПиломатериалаПриИзменении

&НаСервере
Процедура ШиринаПриИзмененииНаСервере()
	Справочники.ВидыПиломатериалов.СформироватьНаименование(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ШиринаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЕдиницаШириныПриИзмененииНаСервере()
	Справочники.ВидыПиломатериалов.СформироватьНаименование(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаШириныПриИзменении(Элемент)
	ЕдиницаШириныПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВысотаПриИзмененииНаСервере()
	Справочники.ВидыПиломатериалов.СформироватьНаименование(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	ВысотаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЕдиницаВысотыПриИзмененииНаСервере()
	Справочники.ВидыПиломатериалов.СформироватьНаименование(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаВысотыПриИзменении(Элемент)
	ЕдиницаВысотыПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДлинаПриИзмененииНаСервере()
	Справочники.ВидыПиломатериалов.СформироватьНаименование(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	ДлинаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЕдиницаДлиныПриИзмененииНаСервере()
	Справочники.ВидыПиломатериалов.СформироватьНаименование(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаДлиныПриИзменении(Элемент)
	ЕдиницаДлиныПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти