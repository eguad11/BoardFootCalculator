#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Перед записью следует сформировать наименование пиломатериала
// согласно выбранным параметрам.
//
Процедура СформироватьНаименование(ТекущийОбъект) Экспорт

	Если ТекущийОбъект.ТипПиломатериала <> Перечисления.ТипыПиломатериалов.ПустаяСсылка() Тогда
		
		ФорматЧисла = "ЧРД=,; ЧРГ=''''; ЧН=-; ЧГ=3,0";
		Рез = "";
		ЕдБазовая = Константы.БазоваяЕдиницаИзмерения.Получить();
		
		// Учитываем ширину
		//
		тШирина = ЕдиницыИзмеренияНаСервере.ПересчитатьЗначение(ТекущийОбъект.Ширина, ТекущийОбъект.ЕдиницаШирины, ЕдБазовая);
		Рез = Формат(тШирина, ФорматЧисла) + "*";
		
		// Учитываем высоту
		//
		Если ТекущийОбъект.ТипПиломатериала <> Перечисления.ТипыПиломатериалов.Бревно Тогда
			тВысота = ЕдиницыИзмеренияНаСервере.ПересчитатьЗначение(ТекущийОбъект.Высота, ТекущийОбъект.ЕдиницаВысоты, ЕдБазовая);
			Рез = Рез + Формат(тВысота, ФорматЧисла) + "*";
		КонецЕсли;
		
		// Учитываем длину
		//
		тДлина = ЕдиницыИзмеренияНаСервере.ПересчитатьЗначение(ТекущийОбъект.Длина, ТекущийОбъект.ЕдиницаДлины, ЕдБазовая);
		Рез = Рез + Формат(тДлина, ФорматЧисла);
		
		// Добавляем наименование типа
		//
		Рез = Строка(ТекущийОбъект.ТипПиломатериала) + " " + Рез + " " + Строка(ЕдБазовая);
		
		// Обновление значения наименования только при условии реального отличия
		//
		Если ТекущийОбъект.Наименование <> Рез Тогда
			ТекущийОбъект.Наименование = Рез;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // СформироватьНаименование

#КонецЕсли
 