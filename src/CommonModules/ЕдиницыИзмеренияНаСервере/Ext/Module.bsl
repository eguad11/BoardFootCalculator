
// Возвращает таблицу соответствия единиц измерения коэффициентам
// для пересчёта одной идиницы измерения в другую.
//
Функция ТаблицаКоэффициентовЕдиницИзмерения() Экспорт
	
	Таб = Новый ТаблицаЗначений;
	Таб.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("ПеречислениеСсылка.ЕдиницыИзмерения"));
	Таб.Колонки.Добавить("Коэффициент", Новый ОписаниеТипов("Число"));
	
	Стр = Таб.Добавить();
	Стр.ЕдиницаИзмерения 	= Перечисления.ЕдиницыИзмерения.Миллиметр;
	Стр.Коэффициент 		= 1000;
	
	Стр = Таб.Добавить();
	Стр.ЕдиницаИзмерения 	= Перечисления.ЕдиницыИзмерения.Сантиметр;
	Стр.Коэффициент 		= 100;
	
	Стр = Таб.Добавить();
	Стр.ЕдиницаИзмерения 	= Перечисления.ЕдиницыИзмерения.Метр;
	Стр.Коэффициент 		= 1;
	
	Возврат Таб;
	
КонецФункции // ТаблицаКоэффициентовЕдиницИзмерения

// Пересчитывает значение длины из одной единицы в другую
// согласно таблице коэффициентов.
//
Функция ПересчитатьЗначение(ЗначениеДлины, НачальнаяЕдиница, ТребуемаяЕдиница) Экспорт
	
	ИтогПересчета = 0;
	
	Если ЗначениеДлины <> 0 И НачальнаяЕдиница <> Перечисления.ЕдиницыИзмерения.ПустаяСсылка() И ТребуемаяЕдиница <> Перечисления.ЕдиницыИзмерения.ПустаяСсылка() Тогда
		
		Если НачальнаяЕдиница = ТребуемаяЕдиница Тогда
			
			ИтогПересчета = ЗначениеДлины;
			
		Иначе
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	ТаблицаКоэффициентов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			               |	ТаблицаКоэффициентов.Коэффициент КАК Коэффициент
			               |ПОМЕСТИТЬ втТаблицаКоэффициентов
			               |ИЗ
			               |	&ТаблицаКоэффициентов КАК ТаблицаКоэффициентов
			               |
			               |ИНДЕКСИРОВАТЬ ПО
			               |	ЕдиницаИзмерения
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ ПЕРВЫЕ 1
			               |	втТаблицаКоэффициентов.ЕдиницаИзмерения КАК НачальнаяЕдиница,
			               |	втТаблицаКоэффициентов.Коэффициент КАК НачальныйКоэффициент,
			               |	NULL КАК ТребуемаяЕдиница,
			               |	NULL КАК ТребуемыйКоэффициент
			               |ИЗ
			               |	втТаблицаКоэффициентов КАК втТаблицаКоэффициентов
			               |ГДЕ
			               |	втТаблицаКоэффициентов.ЕдиницаИзмерения = &НачальнаяЕдиница
			               |
			               |ОБЪЕДИНИТЬ ВСЕ
			               |
			               |ВЫБРАТЬ ПЕРВЫЕ 1
			               |	NULL,
			               |	NULL,
			               |	втТаблицаКоэффициентов.ЕдиницаИзмерения,
			               |	втТаблицаКоэффициентов.Коэффициент
			               |ИЗ
			               |	втТаблицаКоэффициентов КАК втТаблицаКоэффициентов
			               |ГДЕ
			               |	втТаблицаКоэффициентов.ЕдиницаИзмерения = &ТребуемаяЕдиница";
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("ТаблицаКоэффициентов", ТаблицаКоэффициентовЕдиницИзмерения());
			Запрос.УстановитьПараметр("НачальнаяЕдиница", НачальнаяЕдиница);
			Запрос.УстановитьПараметр("ТребуемаяЕдиница", ТребуемаяЕдиница);
			
			Рез = Запрос.Выполнить().Выбрать();
			Если Рез.Следующий() Тогда
				НачальныйКоэффициент = Рез.НачальныйКоэффициент;
				Если Рез.Следующий() Тогда
					ТребуемыйКоэффициент = Рез.ТребуемыйКоэффициент;
					
					// Пересчёт по коэффициентам
					Если НачальныйКоэффициент <> 0 И ТребуемыйКоэффициент <> 0 Тогда
						ИтогПересчета = Окр(ЗначениеДлины * ТребуемыйКоэффициент / НачальныйКоэффициент, 4);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИтогПересчета;
	
КонецФункции // ПересчитатьЗначение